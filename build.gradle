plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
    id 'eclipse'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.3.0'
}

apply from: 'functional-test.gradle'

repositories {
	jcenter()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	mavenLocal()
	maven { url 'https://maven.google.com' }
}


group = PLUGIN_GROUP
version = PLUGIN_VERSION


// Adds gradleApi() (compile) (and >should< gradleTestKit() (testCompile) dependencies)
gradlePlugin {
	testSourceSets sourceSets.functionalTest

    plugins {
        pimutdroidPlugin {
            id = 'at.woodstick.pimutdroid'
            implementationClass = 'at.woodstick.pimutdroid.PimutdroidPlugin'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

pitest {
	exportLineCoverage = true
    outputFormats = ["XML", "HTML"]

	excludedMethods = ["toString","equals","hashCode"]
	
	avoidCallsTo = [
		"java.util.logging",
		"org.apache.log4j",
		"org.slf4j",
		"org.apache.commons.logging",
		"org.gradle.api.logging",
	]
	
    mutators = [
        "CONDITIONALS_BOUNDARY",
        "INCREMENTS",
        "INVERT_NEGS",
        "MATH",
        "NEGATE_CONDITIONALS",
        "RETURN_VALS",
        "VOID_METHOD_CALLS",
        "REMOVE_CONDITIONALS",
		"NON_VOID_METHOD_CALLS",
    ]
}

dependencies {
    compile localGroovy()
	
	compile group: "pl.droidsonroids.gradle", name: "gradle-pitest-plugin", version: "0.1.5"
	compile group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-xml", version: "2.9.2"
	
	testCompile gradleTestKit()
	
	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'org.assertj:assertj-core:3.8.0'
	testCompile 'org.easymock:easymock:3.5.1'
	testCompile 'org.apache.commons:commons-lang3:3.7'
	testCompile 'com.github.stefanbirkner:system-rules:1.18.0'
	
	testCompile 'com.netflix.nebula:nebula-test:6.3.0'
	testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task cleanNebula(type: Delete) {
	group "build"
	description "Deletes the nebula test directory."
	
	delete "${project.buildDir}/nebulatest"
}

task startEmulator() {
	doLast {
		Runtime.getRuntime().exec("cmd /c scripts\\start-emulator.bat ${EMULATOR_PORT} ${EMULATOR_NAME}");
	}
}

task stopEmulator(type: Exec) {
	commandLine "cmd", "/c", "scripts\\kill-emulator.bat ${EMULATOR_PORT}"
}



eclipse {
	classpath {
		downloadJavadoc = true
		downloadSources = true
	}
}

/*
import org.gradle.plugins.ide.eclipse.model.*

private void addGradleSourceDeps() {
  project.extensions.eclipse.classpath.file.whenMerged { Classpath cp ->
    String gradleHome = gradle.getGradleHomeDir().absolutePath.replace(File.separator, '/')
    String gradleSrc = "${gradleHome}/src"
    cp.entries.each {ClasspathEntry entry ->
      if ((entry in AbstractLibrary) && (entry.library.file.name.startsWith('gradle-'))) {
        def matcher = entry.library.file.name =~ "gradle-(.*)-(.*)"
        if(matcher) {
	        String sourcePath= gradleSrc + '/' + matcher[0][1];
	        entry.sourcePath = new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory().fromPath(sourcePath)
        }
		println "not matched for ${entry.library.file.name}"
      }
    }
  }
}
addGradleSourceDeps()
*/
